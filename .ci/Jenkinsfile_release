library 'f10-jenkins-library@1.1_patches'
library 'release-library@1.2'

def additionalReleaseActions() {
    // set version in uitest module to RELEASE_VERSION
    sh "mvn org.codehaus.mojo:versions-maven-plugin:2.7:set \
        -DnewVersion=${params.RELEASE_VERSION} \
        -DgenerateBackupPoms=false \
        -f vaadin-flow/samples/test-playground/uitest"
}

def additionalDevelopmentActions() {
    // set version in uitest module to DEVELOPMENT_VERSION
    sh "mvn org.codehaus.mojo:versions-maven-plugin:2.7:set \
        -DnewVersion=${params.DEVELOPMENT_VERSION} \
        -DgenerateBackupPoms=false \
        -f vaadin-flow/samples/test-playground/uitest"
}

pipeline {
   agent any

    options {
        skipDefaultCheckout true
    }

    environment {
        PROJECT_NAME = 'linkki'
        BRANCH_NAME = "${env.GIT_LOCAL_BRANCH}"
        PROJECT_ID = "${PROJECT_NAME}-${BRANCH_NAME.replaceAll(/[^A-Za-z0-9]/, '-').toLowerCase()}"
        NETWORK_NAME = "network-${PROJECT_ID}"
        DEPLOYMENT_NAME = "linkki-sample-test-playground-vaadin-flow"
        DEPLOYMENT_HOST = "${PROJECT_ID}.dockerhost.i.faktorzehn.de"
        DEPLOYMENT_URL = "http://${DEPLOYMENT_HOST}/${DEPLOYMENT_NAME}"
        BASE_IMAGE = 'spring:24.7'
    }

    stages {
        stage('Prepare Release') {
            steps {
                withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                    script {
                        prepareRelease.withAdditionalActions(
                            this.&additionalReleaseActions,
                            this.&additionalDevelopmentActions
                        )
                    }
                }
            }
        }

        stage('Build') {
            steps {
                withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                    sh 'mvn -U -P production clean javadoc:jar source:jar install'
                }

                junit "**/target/surefire-reports/*.xml"
                recordIssues enabledForFailure: true, qualityGates: [[threshold: 1, type: 'NEW', unstable: true]], tools: [java(), javaDoc(), spotBugs(), checkStyle()]
                jacoco sourceInclusionPattern: '**/*.java'
            }
        }

        stage('Dependency-Check') {
            steps {
                withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                    dependencyCheck version: '24.7'
                }
            }
        }

        stage('Check Git Diffs') {
            steps {
                verifyNoChangedFiles()
            }
        }

        stage('Remove Docker Network') {
            steps {
                deleteDockerNetwork "${NETWORK_NAME}"
            }
        }

        stage('Docker Deployment') {
            steps {
                sh 'bash -x .ci/docker_setup.sh ${PROJECT_ID} ${BASE_IMAGE}'

                rtp parserName: 'HTML', nullAction: '1', stableText: """
                    <h3>Sample Deployments</h3>
                    <ul>
                        <li><a href='${DEPLOYMENT_URL}' target="_blank">Playground</a></li>
                    </ul>
                """
            }
        }

        // wait for the sample applications to be ready to run the tests
        stage('Wait for Server') {
            steps {
                waitForServer "${DEPLOYMENT_URL}"
            }
        }

        stage('UI Test') {
            // Run Tests only if build was successful
            when {
                expression {
                    return currentBuild.currentResult == "SUCCESS"
                }
            }

            steps {
                withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                    sh 'mvn -f vaadin-flow/samples/test-playground/uitest/pom.xml test \
                        -Dmaven.test.failure.ignore=true -Dsurefire.rerunFailingTestsCount=3'
                }

                archiveArtifacts allowEmptyArchive: true, artifacts: 'vaadin-flow/samples/test-playground/uitest/target/error-screenshots/*.png'
                junit 'vaadin-flow/samples/test-playground/uitest/target/surefire-reports-*/*.xml'
            }

            environment {
                MAVEN_OPTS = '-Xmx2g -Dtest.hostname=${DEPLOYMENT_HOST} -Dtest.port=80 -Dtest.path=${DEPLOYMENT_NAME}'
            }
        }

        stage('Upload Release') {
            steps {
                script {
                    uploadRelease() {
                        withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                            // only publish final releases (no rcs & milestones) on maven central
                            if(params.RELEASE_VERSION ==~ /(\d+\.)+\d+/) {
                                sh 'mvn javadoc:jar source:jar deploy -PMavenCentralRelease -Ddoc.user=doc'
                            } else {
                                sh 'mvn javadoc:jar source:jar deploy -Ddoc.user=doc'
                            }
                        }
                    }
                }
            }
        }
    }

    post {
        unsuccessful {
            emailext to: env.TEAM_EMAIL, mimeType: 'text/html', subject: 'Jenkins Release Failure - $JOB_NAME', body: '''
                <img src="https://jenkins.io/images/logos/fire/fire.png" style="max-width: 300px;" alt="Jenkins is not happy about it ...">
                <br>
                $BUILD_URL
            '''
        }
    }
}
