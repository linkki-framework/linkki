pipeline {
    agent any

    environment {
        BUILD_NAME = "vaadin14"
        DOCKER_HOST = "ssh://docker@dockerhost.i.faktorzehn.de"
        CONTAINER_RETENTION = 'permanent-on'
    }

    stages {

        stage('Build') {
            steps {
                script {
                    currentBuild.displayName = "#${env.BUILD_NUMBER}.${env.BUILD_NAME}"
                }

                withMaven(maven: 'maven 3.5', jdk: 'OpenJDK 8', mavenLocalRepo: '.repository', publisherStrategy: 'EXPLICIT') {
                    sh 'mvn -U -T 6 -Pproduction -Ddoc.user=doc clean source:jar javadoc:jar deploy'
                    sh 'mvn -f vaadin14/samples/test-playground/uitest/pom.xml clean compile'
                }

                withMaven(maven: 'maven 3.5', jdk: 'OpenJDK 11', mavenLocalRepo: '.repository', publisherStrategy: 'EXPLICIT') {
                    sh 'mvn -T 6 -Dsurefire.directory=surefire-reports-java11 test'
                }
            }

            environment {
                MAVEN_OPTS = '-Xmx768m'
            }
        }

        // deploy all sample applications that are necessary for the ui tests
        stage('Docker Deployment') {
            steps {
                sh 'bash -x .ci/docker_setup.sh ${BUILD_NAME}'
            }
        }

        // wait for the sample applications to be ready to run the ui tests
        stage('Wait for Server') {
            steps {
                // wait with the ui tests until the server is ready
                timeout(time: 3, unit: 'MINUTES') {
                    waitUntil {
                        script {
                            try {
                                sh 'curl --fail http://linkki-${BUILD_NAME}.dockerhost.i.faktorzehn.de/linkki-sample-test-playground-vaadin14/'
                                return true
                            } catch (Exception e) {
                                return false
                            }
                        }
                    }
                }
            }
        }

        // parallel run of UI tests, SonarQube analysis and the deployment
        // of all other sample applications
        stage('UI Test and SonarQube Analysis') {
            parallel {

                // run all ui tests against the deployed sample application(s)
                stage('UI Test v14') {
                    when {
                        expression {
                            currentBuild.currentResult == "SUCCESS"
                        }
                    }

                    steps {
                        withMaven(maven: 'maven 3.5', jdk: 'OpenJDK 8', mavenLocalRepo: '.repository', publisherStrategy: 'EXPLICIT') {
                            sh 'mvn -f vaadin14/samples/test-playground/uitest/pom.xml test'
                        }
                    }

                    environment {
                        MAVEN_OPTS = '-Xmx2g -Dtest.hostname=linkki-${BUILD_NAME}.dockerhost.i.faktorzehn.de -Dtest.port=80'
                    }
                }

                // run the SonarQube analysis for this change
                stage('SonarQube Analysis') {
                    environment {
                        SONAR_LOGIN = credentials('sonar.login')
                    }
                    steps {
                        catchError(buildResult: 'SUCCESS', stageResult: 'FAILURE') {
                            withSonarQubeEnv('SonarQube') {
                                withMaven(maven: 'maven 3.5', jdk: 'OpenJDK 8', mavenLocalRepo: '.repository', publisherStrategy: 'EXPLICIT') {
                                    sh 'mvn -Dsonar.projectName=linkki-$BUILD_NAME -Dsonar.projectKey=linkki-$BUILD_NAME -Dsonar.login=$SONAR_LOGIN sonar:sonar'
                                }
                            }

                            rtp parserName: 'HTML', nullAction: '1', stableText: """
                                <h3>SonarQube</h3>
                                <a href="https://sonarqube.faktorzehn.de/dashboard?id=linkki-${env.BUILD_NAME}" target="_blank">https://sonarqube.faktorzehn.de/dashboard?id=linkki-${env.BUILD_NAME}</a>
                            """
                        }
                    }
                }

                stage('Docker Deployment') {
                    steps {
                        rtp parserName: 'HTML', nullAction: '1', stableText: """
                            <h3>Vaadin 14 samples</h3>
                            <ul>
                                <li><a href="http://linkki-${env.BUILD_NAME}.dockerhost.i.faktorzehn.de/linkki-sample-test-playground-vaadin14">test playground</a></li>
                            </ul>
                        """
                    }
                }

            } // end parallel
        } // end stage 'UI Test and SonarQube Analysis'

        // check if the SonarQube quality gate is fulfilled
        stage('SonarQube Quality Gate') {
            steps {
                timeout(time: 1, unit: 'HOURS') {
                    // set build to unstable if quality gate fails
                    catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                        waitForQualityGate abortPipeline: true
                    }
                }
            }
        }

    }

    post {
        always {
            junit '**/target/surefire-reports/*.xml'
            recordIssues enabledForFailure: true, qualityGates: [[threshold: 1, type: 'NEW', unstable: true]], tools: [java(), javaDoc(), spotBugs(), checkStyle()]
            jacoco sourceInclusionPattern: '**/*.java'

            junit '**/target/surefire-reports-java11/*.xml'

            archiveArtifacts 'vaadin14/doc/target/linkki-core-documentation*/**/*.*, vaadin14/samples/test-playground/uitest/target/uitest/*.png'
        }

        unsuccessful {
            emailext to: env.TEAM_EMAIL, mimeType: 'text/html', subject: 'Jenkins Build Failure - $PROJECT_NAME', body: '''
                <img src="https://jenkins.io/images/logos/fire/fire.png" style="max-width: 300px;" alt="Jenkins is not happy about it ...">
                <br>
                $BUILD_URL
            '''
        }
    }

    options {
        buildDiscarder(logRotator(daysToKeepStr: '14', numToKeepStr: '30'))
    }
}