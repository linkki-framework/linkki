library 'f10-jenkins-library@1.1_patches'

pipeline {
    agent any

    environment {
        PROJECT_NAME = 'linkki'
        BRANCH_NAME = "${env.GIT_LOCAL_BRANCH}"
        PROJECT_ID = "${PROJECT_NAME}-${BRANCH_NAME.replaceAll(/[^A-Za-z0-9]/, '-').toLowerCase()}"
        NETWORK_NAME = "network-${PROJECT_ID}"
        DEPLOYMENT_NAME = "linkki-sample-test-playground-vaadin-flow"
        DEPLOYMENT_HOST = "${PROJECT_ID}.dockerhost.i.faktorzehn.de"
        DEPLOYMENT_URL = "http://${DEPLOYMENT_HOST}/${DEPLOYMENT_NAME}"
    }

    stages {
        stage('Pre-Build') {
            steps {
                script {
                    currentBuild.displayName = "#${env.BUILD_NUMBER}.${env.BRANCH_NAME}"
                }
            }
        }

        stage('Build') {
            steps {
                withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                    sh 'mvn -U -T 6 \
                        -pl "!vaadin-flow/doc" \
                        -Pproduction \
                        clean source:jar javadoc:jar deploy'
                }
            }
        }

        stage('Remove Docker Network') {
            steps {
                deleteDockerNetwork "${NETWORK_NAME}"
            }
        }

        stage('Docker Deployment') {
            steps {
                sh 'bash -x .ci/docker_setup.sh ${PROJECT_ID}'

                rtp parserName: 'HTML', nullAction: '1', stableText: """
                    <h3>Sample Deployments</h3>
                    <ul>
                        <li><a href='${DEPLOYMENT_URL}' target="_blank">Playground</a></li>
                    </ul>
                """
            }
        }

        stage('Upload Documentation') {
            steps {
                withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                    sh 'mvn -U -T 6 \
                        -pl "vaadin-flow/doc" \
                        deploy \
                        -Ddoc.user=doc'
                }
            }
        }

        // wait for the sample applications to be ready to run the tests
        stage('Wait for Server') {
            steps {
                waitForServer "${DEPLOYMENT_URL}"
            }
        }

        stage('Tests') {
            // Run Tests only if build was successful
            when {
                expression {
                    return currentBuild.currentResult == "SUCCESS"
                }
            }
            parallel {
                stage('UI Test') {
                    steps {
                        withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                            sh 'mvn -f vaadin-flow/samples/test-playground/uitest/pom.xml test \
                                -Dmaven.test.failure.ignore=true -Dsurefire.rerunFailingTestsCount=3'
                        }

                        archiveArtifacts allowEmptyArchive: true, artifacts: 'vaadin-flow/samples/test-playground/uitest/target/error-screenshots/*.png'
                        junit 'vaadin-flow/samples/test-playground/uitest/target/surefire-reports-*/*.xml'
                    }

                    environment {
                        MAVEN_OPTS = '-Xmx2g -Dtest.hostname=${DEPLOYMENT_HOST} -Dtest.port=80 -Dtest.path=${DEPLOYMENT_NAME}'
                    }
                }

                // run the SonarQube analysis for this change
                stage('SonarQube Analysis') {
                    steps {
                        withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17') {
                            runSonarQubeAnalysis()
                        }
                    }
                }

                stage('Dependency-Check') {
                    environment {
                        SUITE_VERSION = '24.1'
                    }
                    steps {
                        withMaven(maven: 'maven 3.9', jdk: 'OpenJDK 17', publisherStrategy: 'EXPLICIT') {
                            dependencyCheck version: "${SUITE_VERSION}"
                        }

                        script {
                            def cves = sh script: 'grep -Po "data-id-to-suppress=\\"CVE-\\d*-\\d*" target/dependency-check-report.html | sed -e "s,.*\\(CVE.*\\),\\1," | sort | uniq | tr "\\n" "+"', returnStdout: true
                            echo "CVEs: ${cves}"

                            def globalTemporarySuppressions = "https://gerrit.faktorzehn.de/plugins/gitiles/platform/dependency-check-suppressions/%2B/refs/heads/main/${SUITE_VERSION}/global/suppressions-temporary.xml"
                            def versionUpdatePipeline = "https://druiden-ci.faktorzehn.dev/view/Version%20Update/job/version-update-pipeline/build"
                            def commonsIssues = "https://jira.convista.com/issues/?jql=text~%22${cves}%22%20and%20project%3D%22F10%20Commons%22"
                            def createJiraIssueLink = "https://jira.convista.com/secure/CreateIssueDetails!init.jspa?pid=15370&issuetype=1&summary=${cves}&components=20998&priority=6&customfield_10070=2&customfield_10115=3747&description=h2.+Nach+der+Issue+Erstellung%0D*+*Affected+Version+eintragen*:+letzte+Patchversion%0D+*+*Rank+to+top*%0D%0Dh2.+Umsetzung%0D%23+[Version+Update+Pipeline|${versionUpdatePipeline}]+laufen+lassen+(Branch:+*${BRANCH_NAME}*)%0D+%23+Nicht+resolved?+->+Schauen,+was+*Seals*+gemacht+haben%0D+**+[global+temporary+suppressions|${globalTemporarySuppressions}]%0D+**+[Commons+Issues|${commonsIssues}]%0D%0D[Druiden+CVE+Guideline|https://wiki.convista.com/x/pw_lGg%23Entwicklung%3ACVEinDependencies%2FDependencyCheck-UmsetzungvonCVEIssue%2FSubtask]"

                            echo "Failure? -> Create new JIRA issue ${createJiraIssueLink}"
                        }
                    }
                }
            }
        }

        stage('Check Git Diffs') {
            steps {
                verifyNoChangedFiles()
            }
        }

        // check if the SonarQube quality gate is fulfilled
        stage('SonarQube Quality Gate') {
            steps {
                timeout(time: 1, unit: 'HOURS') {
                    script {
                        def qg = waitForQualityGate()
                        if (qg.status != 'OK') {
                            unstable("SonarQube failed with status: ${qg.status}")
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            junit '**/target/surefire-reports/*.xml'
            recordIssues enabledForFailure: true, qualityGates: [[threshold: 1, type: 'NEW', unstable: true]], tools: [java(), javaDoc(), spotBugs(), checkStyle()]
            jacoco sourceInclusionPattern: '**/*.java'

            archiveArtifacts 'vaadin-flow/doc/target/doc/**'
        }

        regression {
            emailext to: '${REGRESSION_EMAIL_LINKKI}', mimeType: 'text/html', subject: 'Jenkins Build Failure - $PROJECT_NAME', body: '''
                <img src="https://jenkins.io/images/logos/fire/fire.png" style="max-width: 300px;" alt="Jenkins is not happy about it ...">
                <br>
                $BUILD_URL
            '''
        }
    }

    options {
        buildDiscarder(logRotator(daysToKeepStr: '14', numToKeepStr: '30'))
    }
}
